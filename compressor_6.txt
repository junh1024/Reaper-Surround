desc: slow symmetrical surround compressor
//named according to https://dictionary.cambridge.org/grammar/british-grammar/about-adjectives-and-adverbs/adjectives-order
//gfx section modified from Liteon's deesser (GPL)

slider1:-3<-32,0,1>Threshold
slider2:2048<128,4096,128>Blocksize
slider3:0.3<0.05,0.5,0.05>Responsiveness
slider4:6<1,16,1>Channels

@init

count=0;
sumALL=0;
gain_excess=0;
gain_old=1;
threshold_exceeded_number=0;
abs_spl=0;
gain=1;

blocksize=slider2;
gain_reduction=0;

//GFX variables
sqrt2 = sqrt(2);
s2 = sqrt2/2;
// gr_meter_decay = exp(1/(1*srate));


@slider
threshold=2 ^ (slider1/6); //in dB
blocksize=slider2;
response=slider3;
chans=slider4;

@block
count+=samplesblock;

(count>blocksize)?
(
	average_spls=(sumALL/threshold_exceeded_number);
	average_spls>threshold? //if the average is over the threshold
	(gain=threshold/average_spls;) //set the gain so that the audio would not be >threshold
	:
	(gain=1;);
	
	sumALL=0;
	count=0;
	threshold_exceeded_number=0;
);



gain=(gain*response )+ gain_old*(1-response);
gr_meter=gain;


gain_old=gain;


@sample

sumALL_current=0;
n = 0;

//add all chans to a temp
loop(chans,
	abs_spl=spl(n)*sign(spl(n)); //get a positive value every time
	sumALL_current+=abs_spl;
	
	spl(n)*=gain; //apply current gain

	n+=1;
);

//add only to sumALL if threshold exceeded
sumALL_current>(threshold*chans)?
(
sumALL+=sumALL_current;
threshold_exceeded_number+=chans;
);


// );

@gfx 425 16
//---------------------------------
// set gr meter
//---------------------------------
// gr_meter *= exp(1/30);
gr_meter > 1 ? gr_meter=1;
gfx_r=0.6;
gfx_g=0.4;
gfx_b=0.5;
gfx_a=0.8;
meter_bot=20;
meter_h=min(gfx_h,21);
xscale=gfx_w*20/meter_bot;
gfx_y=0;
gfx_x=gfx_w + log10(gr_meter)*xscale;
gfx_rectto(gfx_w,meter_h);
//---------------------------------
// draw scale
//---------------------------------
gfx_r=1;
gfx_b=1;
gfx_g=1;
gfx_a=0.6;
g = s2;
while(
gfx_x=gfx_w + log10(g)*xscale;
gfx_x >= 0 ? (
gfx_y=0;
gfx_lineto(gfx_x,meter_h-1,0);
gfx_y=meter_h-gfx_texth-5;
gfx_x+=4;
gfx_drawnumber(log10(g)*20,0);
gfx_drawchar($'d');
gfx_drawchar($'B');
);
g*=s2;
gfx_x >=0;
);
gfx_x=0;
gfx_y=meter_h;
gfx_lineto(gfx_w,meter_h,0);
gfx_a=0.9;
gfx_x=gfx_w - 61;
gfx_y=meter_h + gfx_texth - 1;
