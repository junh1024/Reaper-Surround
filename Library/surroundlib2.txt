desc: medium functions

@init

coeff_A=0;
coeff_B=0;
coeff_C=0;
coeff_D=0;

//scaled from 0-100 to 0>half pi, see 5.1 mix control
function calculate_depth_coeffs(depth_coeff)
(
	(  depth_coeff >= 0 )? 
	(
	  coeff_A = cos( depth_coeff - ($pi/2)  );
	 ):
	(
	  coeff_A = cos( depth_coeff + ($pi/2) ); 
	);
	 coeff_B = cos( depth_coeff );
 );
 
function calculate_pan_coeffs(pan_coeff)
(
	//Pan ref
	(  pan_coeff >= 0 )?  (
		coeff_C =cos( pan_coeff - ($pi/2)  ) ;
	):
	(
		coeff_C = cos( pan_coeff + ($pi/2) ) ; 
	);
		coeff_D = cos( pan_coeff );
);



function do_depth(depth_coeff)
(
	(  depth_coeff >= 0 )?  (
	  spl0 += coeff_A*spl4;
	  spl4 = coeff_B*spl4;

	  spl1 += coeff_A*spl5;
	  spl5 = coeff_B*spl5;
	):
	(
	  spl4 += coeff_A*spl0; //reversed order is important
	  spl0 = coeff_B*spl0;
	  spl5 += coeff_A*spl1; 
	  spl1 = coeff_B*spl1;
	);
);

function do_pan(pan_coeff)
(
	(  pan_coeff >= 0 )?  (
		spl1 += coeff_C  *spl0;
		spl0 =  coeff_D *spl0;

		spl5 += coeff_C  *spl5;
		spl4 =  coeff_D *spl4;
	):
	(
		spl0 += coeff_C*spl1; //reversed order is important
		spl1 =  coeff_D*spl1;
		spl4 += coeff_C*spl5; 
		spl5 =  coeff_D*spl5;
	);
);

function do_side_physical_level(side_physical_level)
(
	spl0+=(spl6)*0.5*(1-side_physical_level);
	spl1+=(spl7)*0.5*(1-side_physical_level);
	spl4+=(spl6)*0.5*(1-side_physical_level);
	spl5+=(spl7)*0.5*(1-side_physical_level);
	spl6*=side_physical_level;
	spl7*=side_physical_level;
);

function downmix_151_71_v1()
(
	spl0 += spl8 + spl10; //top C & LFE are a L & R of a new bottom layer
	spl1 += spl9 + spl11;
	spl4 += spl12;
	spl5 += spl13;
	spl6 += spl14;
	spl7 += spl15;
);

function silence_top_layer_151()
(
	spl8 = 0;
	spl9 = 0;
	spl10= 0;
	spl11= 0;
	spl12= 0;
	spl13= 0;
	spl14= 0;
	spl15= 0;
);

function MS_encode_ELL()
(
	front_M =(spl0+spl1)*0.5;
	front_S =(spl0-spl1)*0.5; 
	back_M  =(spl4+spl5)*0.5;
	back_S  =(spl4-spl5)*0.5; 
	side_M  =(spl6+spl7)*0.5;
	side_S  =(spl6-spl7)*0.5; 
);

function MS_encode_Top()
(
	top_front_M =(spl8+spl9)*0.5;
	top_front_S =(spl8-spl9)*0.5; 
	top_back_M  =(spl12+spl13)*0.5;
	top_back_S  =(spl12-spl13)*0.5; 
	top_side_M  =(spl14+spl15)*0.5;
	top_side_S  =(spl14-spl15)*0.5; 
);


function ELL_Side_Depth_71_to_71(ELL_Side_Depth)
(
	ELL_Side_Depth>0?(
	//ELL side depth
	spl0 +=(spl6)*   ELL_Side_Depth;
	spl1 +=(spl7)*   ELL_Side_Depth;
	spl6=  (spl6)*(1-ELL_Side_Depth);
	spl7=  (spl7)*(1-ELL_Side_Depth);
	)
	:
	(
	ELL_Side_Depth*=-1;
	spl4 +=(spl6)*  ELL_Side_Depth;
	spl5 +=(spl7)*  ELL_Side_Depth;
	spl6= (spl6)*(1-ELL_Side_Depth);
	spl7= (spl7)*(1-ELL_Side_Depth);
	);
);

function Top_Side_Depth_71_to_71(Top_Side_Depth)
(
	Top_Side_Depth>0?(
	//ELL side depth
	spl8 +=(spl14)*   Top_Side_Depth;
	spl9 +=(spl15)*   Top_Side_Depth;
	spl14= (spl14)*(1-Top_Side_Depth);
	spl15= (spl15)*(1-Top_Side_Depth);
	)
	:
	(
	Top_Side_Depth*=-1;
	spl12 +=(spl14)*  Top_Side_Depth;
	spl13 +=(spl15)*  Top_Side_Depth;
	spl14= (spl14)*(1-Top_Side_Depth);
	spl15= (spl15)*(1-Top_Side_Depth);
	);
);

delaylen=0;

function calculate_delay_2ch(delay_slider_ms)
(
	delaylen=delay_slider_ms*srate/1000;
);

function delay_2ch(chan_offset)
(
	dpintA = delayposA*2;
	dpintB = delayposB*2;

	os0 = dpintA[0];
	os1 = dpintB[1];

	dpintA[0] = spl(chan_offset);
	dpintB[1] = spl(chan_offset+1);

	(delayposA+=1) >= delaylen ? delayposA=0;
	(delayposB+=1) >= delaylen ? delayposB=0;

	spl(chan_offset) = os0;
	spl(chan_offset+1) = os1;
);


delta=0.01;

sumMid=0+delta;
sumSide=0;


function MidSide_stats_collect(ch_offs_ms,ch_offs_ms2)
(
	Mid=( spl(ch_offs_ms)+spl(ch_offs_ms2))*0.5;//compute Mid/Side
	Side=(spl(ch_offs_ms)-spl(ch_offs_ms2))*0.5;

	Mid_abs =Mid*sign(Mid) ; //get magnitude
	Side_abs=Side*sign(Side) ;
		
	sumMid+=Mid_abs;
	sumSide+=Side_abs;

);

function MidSide_stats_analyze(pan,response)
(
	newpan=(((sumSide)/(sumMid))*response)+(pan*(1-response));

	sumMid=0+delta;
	sumSide=0;
	
	min(newpan,1);
);