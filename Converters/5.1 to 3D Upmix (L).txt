// from https://forum.cockos.com/showthread.php?t=190168#30 by Tale, geraintluff, junh1024

desc: 5.1 to 5.1.4 (padded to 15.1) upmix

slider1:1<0,4,1{Ambience,Ambience +,Discrete SFX,Discrete SFX + (N/A),Pan Slice,Function Designer}>Mode
slider2:100<0,200,1>Amount (%)
slider3:0<-1,1,0.05>Threshold/Pan
slider4:0.2<0,1,0.05>Crossover/Width
slider5:75<0,100,1>Frequency Cutoff (%)
slider6:50<0,100,2>Time Response 
slider7:25<0,50,5>FFT Overlap (%)
slider8:12<10,14,1>FFT size (pow2)




// import surroundlib2.txt
import surroundlib3.txt
import surroundlibf.txt

@init

//see surroundlibf.txt

function testpan(thepan,xo)
(
	// scaling=min(abs (   max(thepan-(xo/2),0)*(0.5/xo) )  ,1);
	scaling=min( max( abs(thepan-(Pan ))*(1/xo) -xo  ,0)  ,1 );
	scaling;
);

pdc_bot_ch = 0;
pdc_top_ch = 14;

out0=out1=out4=out5=0;

@slider
sliderfft = (2^(slider8))|0;
fftsize != sliderfft ? fft_initialize();


//parameter migration from old version, which had Frequency & Time response 0-1 instead of 0-100
slider6<2?slider6*=100;
Time_Response=slider6/100;

Amount=slider2/100;


mode=slider1+1;//cuz I don't like 0-based mode/slider


mode<3? //  >100% amount will make it louder in ambience modes.
(
	Amount=min(Amount,1); //clamp to 100%
);

Pan=slider3;
Crossover=slider4;

Resp_bins=slider5/100;

elements_to_skip=fftsize-floor(Resp_bins*fftsize);




@sample


sum_all_spl+=abs(spl0)+abs(spl1)+abs(spl4)+abs(spl5);	//silence detector

pos >= fftsize ?
(
	tmp = buf1_a;
	buf1_a = buf1_b;
	buf1_b = tmp;

	tmp = buf2_a;
	buf2_a = buf2_b;
	buf2_b = tmp;

	check_silence_set_fftsize(0); //reduce CPU
	
	fft(buf1_a, fftsize);
	fft_permute(buf1_a, fftsize);
	
	fft(buf2_a, fftsize);
	fft_permute(buf2_a, fftsize);
	
	//silence the frequencies we are skipping
	memset(buf1_a+(fftsize-elements_to_skip), 0, 2*elements_to_skip);
	memset(buf2_a+(fftsize-elements_to_skip), 0, 2*elements_to_skip);	

	i = 0;
	loop(fftsize / 2 + 1 - (elements_to_skip/2),
	

		a = i;
		b = a+1;
		a2 = 2*fftsize-i;
		b2 = a2+1;
 
		fft_4ch_untangle_mags_2();

		// p1=abs(Magnitudes3[i]-Magnitudes1[i])/(Magnitudes3[i]+Magnitudes1[i]); //for FPA & min2


		mode==16? //subtract
		(
			progress=i/(fftsize/2);
			
			//clamp mag to >0, otherwise will make more sound
			//subtract more, as the frequency increases
			L_scaling_array[i]=max(Magnitudes1[i]-Magnitudes3[i]*(Amount+((1-Response))*progress),0)/Magnitudes1[i];
			R_scaling_array[i]=max(Magnitudes2[i]-Magnitudes4[i]*(Amount+((1-Response))*progress),0)/Magnitudes2[i];
			
			//temporal averaging for <12khz to sound better, none for >12khz to remove transients. inspired by my newvisp4
			//but too much tends to reduce volume
			
			//if it;s <x khz & not a transient, apply averaging to redude artefacts
			i<(fftsize/3) && L_scaling_array[i] < L_scaling_old_array[i]? 
			(
				L_scaling=L_scaling_array[i]*(0.5)+L_scaling_old_array[i]*(1-0.5);
				R_scaling=R_scaling_array[i]*(0.5)+R_scaling_old_array[i]*(1-0.5);
				
				// L_scaling =L_scaling_array[i];
				// R_scaling =R_scaling_array[i];
			)
			:
			(	//equivalent to response of 1
				L_scaling =L_scaling_array[i];
				R_scaling =R_scaling_array[i];
			);
			
			L_scaling_old_array[i]=L_scaling_array[i]*(0.5)+L_scaling_old_array[i]*(1-0.5);
			R_scaling_old_array[i]=R_scaling_array[i]*(0.5)+R_scaling_old_array[i]*(1-0.5);

			lr_complex_apply_2_scaling(L_scaling, R_scaling);
			


			// inspector1=(1-response);
			// inspector2=(1-response)*progress;
			
		);
		
		
		
		(mode==10)? //Min2
		(
			
			//compute pans as per my V3 upmix
			// p1=(Magnitudes3[i]-Magnitudes1[i])/(Magnitudes3[i]+Magnitudes1[i]);
			p2=(Magnitudes4[i]-Magnitudes2[i])/(Magnitudes4[i]+Magnitudes2[i]);
		//pan isn't that useful for dual stereo-different noise recordings, but useful for High Touch! (different singers on different versions)

			
			Angle_Difference1[i]>pi?Angle_Difference1[i]=2*pi-Angle_Difference1[i];//fix angle difference cuz should be under 180*
			Angle_Difference2[i]>pi?Angle_Difference2[i]=2*pi-Angle_Difference2[i]; 

			L_scaling=(1-(min(Angle_Difference1[i]*2,pi)/pi))*(1-   (p1));//double ad cuz 90* ad means it's already purely stereo noise
			R_scaling=(1-(min(Angle_Difference2[i]*2,pi)/pi))*(1-abs(p2));//Reduce mags the more it's panned & phased

			left_r *= L_scaling^Amount; //raised to power because doing linear multiple 1- is tricky
			left_i *= L_scaling^Amount;
			right_r *= R_scaling^Amount;
			right_i *= R_scaling^Amount;

		);  
		
		(mode<3)? // ambience modes
		(
			Angles1[i]=atan2(left_r,left_i);
			Angles2[i]=atan2(right_r,right_i);
			Angles3[i]=atan2(left_r_two,left_i_two);
			Angles4[i]=atan2(right_r_two,right_i_two);
			
			Angle_Difference1[i]=abs(Angles1[i]-Angles2[i]); 
			Angle_Difference2[i]=abs(Angles3[i]-Angles4[i]);

			
			Angle_Difference1[i]>pi?Angle_Difference1[i]=2*pi-Angle_Difference1[i];//fix angle difference cuz should be under 180*
			Angle_Difference2[i]>pi?Angle_Difference2[i]=2*pi-Angle_Difference2[i]; 
			
			mode==1? //90* is at the top
			(
				Angle_Difference1[i]>pi/2?Angle_Difference1[i]=pi-Angle_Difference1[i]; //reflect >90*
				Angle_Difference2[i]>pi/2?Angle_Difference2[i]=pi-Angle_Difference2[i]; 
								
				L_scaling_array[i]=1-(Angle_Difference1[i]*Amount/(pi/2));
				R_scaling_array[i]=1-(Angle_Difference2[i]*Amount/(pi/2));
				
				// lr_complex_apply_1_scaling(L_scaling);
				// l2r2_complex_apply_1_scaling(R_scaling);
			
			):
			(	//180* at top
								
				L_scaling_array[i]=1-(Angle_Difference1[i]*Amount/(pi));
				R_scaling_array[i]=1-(Angle_Difference2[i]*Amount/(pi));
			
			);
			
				array_1_index_apply_time_response(L_scaling_array,L_scaling_old_array,i,Time_Response);
				array_1_index_apply_time_response(R_scaling_array,R_scaling_old_array,i,Time_Response);
			
				lr_complex_apply_1_scaling(L_scaling_array[i]);
				l2r2_complex_apply_1_scaling(R_scaling_array[i]);
		
		);
		
		
		
		(mode==3||mode==4)? //measure pos
		( 
		// pos_X=pos_Y=0;		
		sum_all_mags=Magnitudes1[i]+Magnitudes2[i]+Magnitudes3[i]+Magnitudes4[i];
		
		pos_X[i]=(0-Magnitudes1[i]+Magnitudes2[i]-Magnitudes3[i]+Magnitudes4[i])/sum_all_mags;
		pos_Y[i]=(0-Magnitudes1[i]-Magnitudes2[i]+Magnitudes3[i]+Magnitudes4[i])/sum_all_mags;
		
		array_1_index_apply_time_response(pos_X,pos_X_old,i,Time_Response);
		array_1_index_apply_time_response(pos_Y,pos_Y_old,i,Time_Response);
		
		//clamp distance to max of 1
		// distance=min(sqrt(pos_X[i]^2+pos_Y[i]^2),1);
		L_scaling_array[i]=min(abs(pos_X[i])+abs(pos_Y[i]),1); //faster measuring of distance
		
		// L_scaling_array[i]= (L_scaling_array[i]*(Time_Response))+(L_scaling_old_array[i]*(1-Time_Response));

		//raised to power because doing linear multiple 1- is tricky

		
		lr_complex_apply_1_scaling(L_scaling_array[i]^Amount);
		l2r2_complex_apply_1_scaling(L_scaling_array[i]^Amount);

		
		// centerness=distance/(sum_all_mags);
		
		// inspector1=Magnitudes1[i]+Magnitudes2[i]+Magnitudes3[i]+Magnitudes4[i];
		// inspector1=Magnitudes5[i];
		
		// inspector1=L_scaling^Amount;

		// mode4=mags for depth, M/S for phase?
		);
		
		(mode==5)? //pan slice
		(
		
		L_pan[i]=(-Magnitudes3[i]+Magnitudes1[i])/(Magnitudes3[i]+Magnitudes1[i]);
		R_pan[i]=(-Magnitudes4[i]+Magnitudes2[i])/(Magnitudes4[i]+Magnitudes2[i]);
		
		array_1_index_apply_time_response(L_pan,L_pan_old,i,Time_Response);
		array_1_index_apply_time_response(R_pan,R_pan_old,i,Time_Response);
		
		L_scaling_array[i]=min( max( abs(L_pan[i]-(Pan ))*(1/Crossover) -Crossover  ,0)  ,1 );
		R_scaling_array[i]=min( max( abs(R_pan[i]-(Pan ))*(1/Crossover) -Crossover  ,0)  ,1 );
		
		// L_scaling_array[i]= (L_scaling_array[i]*(Time_Response))+(L_scaling_old_array[i]*(1-Time_Response));
		// R_scaling_array[i]= (R_scaling_array[i]*(Time_Response))+(R_scaling_old_array[i]*(1-Time_Response));
		
		
		lr_complex_apply_2_scaling(L_scaling_array[i]*Amount , R_scaling_array[i]*Amount );

		left_r_two *= L_scaling_array[i]*Amount; 
		left_i_two *= L_scaling_array[i]*Amount;

		right_r_two *= R_scaling_array[i]*Amount;
		right_i_two *= R_scaling_array[i]*Amount;
	
			// testpan(Pan,Crossover);

		);

		//more voodoo
		buf1_a[a]  = left_r - right_i;
		buf1_a[b]  = left_i + right_r;
		buf1_a[a2] = left_r + right_i;
		buf1_a[b2] = right_r - left_i;

		buf2_a[a]  = left_r_two - right_i_two;
		buf2_a[b]  = left_i_two + right_r_two;
		buf2_a[a2] = left_r_two + right_i_two;
		buf2_a[b2] = right_r_two - left_i_two;

		i += 2;
	);

	// Inverse FFT
	fft_ipermute(buf1_a, fftsize);
	ifft(buf1_a, fftsize);

	fft_ipermute(buf2_a, fftsize);
	ifft(buf2_a, fftsize);
	
	check_silence_restore_fftsize();
	pos=0;
);


//windowing?
w1 = window[pos/2];
w2 = window[(fftsize-pos)/2-1];
sw = (w1+w2)*fftsize;

//delay c/lfe
out2 = buf3_org[pos2]  ;
out3 = buf3_org[pos2+1];

buf3_org[pos2]	=  spl2;
buf3_org[pos2+1]=  spl3;

spl2=out2;
spl3=out3;

out0 = (buf1_a[pos]+buf1_b[fftsize+pos])/sw;
out1 = (buf1_a[pos+1]+buf1_b[fftsize+pos+1])/sw;
out4 = (buf2_a[pos]+buf2_b[fftsize+pos])/sw;
out5 = (buf2_a[pos+1]+buf2_b[fftsize+pos+1])/sw;

//buffer inputs
buf1_a[pos] = w1*spl0;
buf1_a[pos+1] = w1*spl1;
buf1_b[fftsize+pos] = w2*spl0;
buf1_b[fftsize+pos+1] = w2*spl1;

buf2_a[pos] = w1*spl4;
buf2_a[pos+1] = w1*spl5;
buf2_b[fftsize+pos] = w2*spl4;
buf2_b[fftsize+pos+1] = w2*spl5;

//buffer the originals
out8 = buf1_org[pos2]  ;
out9 = buf1_org[pos2+1];
out12 = buf2_org[pos2]  ;
out13 = buf2_org[pos2+1];

buf1_org[pos2] =  spl0;
buf1_org[pos2+1] =  spl1;
buf2_org[pos2] =  spl4;
buf2_org[pos2+1] =  spl5;

//do output
spl0 = out0;
spl1 = out1;

spl4 = out4;
spl5 = out5;

//subtract processed from original to get height
spl8=(out8-spl0);
spl9=(out9-spl1);

spl12=(out12-spl4);
spl13=(out13-spl5);


pos += 2;
pos2=(pos2+2)%(fftsize*2);


@gfx 500 500

box_width=16;


// gfx_mode=1.0;

g=0;//counter for gfx

gfx_r=gfx_g=gfx_b=gfx_a=1;

gfx_a = 0.5; 


mode==1?
(	//draw guides
	drawbox(gfx_w*0.25,0);
	drawbox(gfx_w*0.75,0);
	drawbox(gfx_w*0.25,gfx_h);
	drawbox(gfx_w*0.75,gfx_h);
	
	/*
	while
	( 
		//trying to make a strip indicating how wide it is by drawing alternating bins on different sides but failing
		set_color(g/(fftsize/2.5)); //update color according to frequency
		drawbox(gfx_w/2+ ((Angle_Difference1[g]*0.5*(-1^(g/8)))/pi) ,0); //draw position for each bin
		// g=ceil(g^1.2);
		g+=8;
		g<fftsize/2 - 1 - (elements_to_skip/2);

	);
	*/
);


mode==2?
(
	drawbox(gfx_w*0,0);
	drawbox(gfx_w*1,0);
	drawbox(gfx_w*0,gfx_h);
	drawbox(gfx_w*1,gfx_h);
	

);

gfx_a = 0.2; 

(mode==3||mode==4)?
(	//draw guides, since it pushes absolute center sounds upwards
	drawbox(gfx_w*0.5,0);
	drawbox(gfx_w*0.5,gfx_h);
	
	drawbox(gfx_w*0,gfx_h*0.5);
	drawbox(gfx_w*1,gfx_h*0.5);
	
	drawbox(gfx_w*0.5,gfx_h*0.5);


	while
	( 
		set_color(g/(fftsize/2.5)); //update color according to frequency
		drawbox((pos_X[g]*gfx_w/2)+gfx_w/2,(pos_Y[g]*gfx_h/2)+gfx_h/2); //draw position for each bin
		// g=ceil(g^1.2);
		g+=8;
		g<fftsize/2 - 1 - (elements_to_skip/2);

	);
);

mode==5?
(
	//draw guides, crossover doesn't need /2 since that's covered by the 2nd *0.5
	drawbox(0,(-Pan+1)*0.5*gfx_h);
	drawbox(0,(-Pan+1-(Crossover))*0.5*gfx_h);
	drawbox(0,(-Pan+1+(Crossover))*0.5*gfx_h);

	drawbox(gfx_w,(-Pan+1)*0.5*gfx_h);
	drawbox(gfx_w,(-Pan+1-(Crossover))*0.5*gfx_h);
	drawbox(gfx_w,(-Pan+1+(Crossover))*0.5*gfx_h);

	
	while
	( 
		set_color(g/(fftsize/2.5)); //update color according to frequency
		drawbox(  gfx_w - ((g*gfx_w)/(fftsize)), (-R_pan[g]+1)*0.5*gfx_h  ) ; //draw position for each bin
		drawbox(  0 + ((g*gfx_w)/(fftsize)), (-L_pan[g]+1)*0.5*gfx_h  ) ; //draw position for each bin
		g+=8;
		g<fftsize/2 - 1 - (elements_to_skip/2);

	);

);


mode==6?
(
	g=-1;
	
	loop(2000,
	drawbox(gfx_w*(g+1)/2, gfx_h-(gfx_h*testpan(g,Crossover)));
	g+=0.001;
	);

);
