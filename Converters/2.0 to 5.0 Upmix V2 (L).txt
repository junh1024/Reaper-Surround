desc: 
//includes code from stock lowpass,

slider1:0<0,100,2>Width
slider2:100<-100,100,2>Depth
slider3:1<0,1,{Reverse (Narrow),Normal (Wide)}>Polarity of SR (Rear width)
slider4:15000<5000,15000,500>Rear LP (Hz, 15k=off)
slider5:0<0,100,1>Rear Delay (ms)
slider6:0<0,1,0.01>Pan
slider7:1024<512,8192,512>Blocksize
slider8:0.1<0.05,1,0.05>Responsiveness

import surroundlib2.txt
import surroundlib3.txt


in_pin:in
in_pin:in
in_pin:in
in_pin:in
in_pin:in
in_pin:in

out_pin:L
out_pin:R
out_pin:C
out_pin:LFE
out_pin:BL
out_pin:BR

@init
//lopass
shape = 0.2 ;  //filter shape (0=sharp, 1=dull)

@slider
//depth
depth_coeff=(slider2*$pi)/200; //depth scaled from 0-100 to 0>half pi
calculate_depth_coeffs(depth_coeff);

blocksize=slider7;
response=slider8;

//lopass
damp=0.01+shape*20;
c = 1/tan($pi*slider4/srate);
fk = 1 / (1 + c*(c+damp));
fa1 = 2 * (1 - c*c) * fk;
fa0 = (1 + c*(c-damp)) * fk;

//delay
// calculate_delay_2ch(slider5);
delaylen=calculate_delay_2ch(slider5);

//polarity
polarity=(slider3-0.5)*2;

//behaviour quirks
// slider3==0?(slider5=10):(slider5=0);
slider5==0?(slider3=1); //if delay is 0, should be wide to reduce artefacts.

@block
count+=samplesblock;

(count>blocksize)?
(
	slider6=MidSide_stats_analyze(slider6,response);
	count=0;
);

// slider6=min((slider1/1000)+slider6,1)

@sample

//changed from 0.5 to 0.7171 in 2024
mid = 0.7071*(spl0+spl1);
side= 0.7071*(spl0-spl1);

//collect stats
Mid_abs =mid*sign(mid) ; //get magnitude
Side_abs=side*sign(side) ;

sumMid+=Mid_abs;
sumSide+=Side_abs;

spl0 = (0.7071*mid*(slider6)) + (side*0.2); 
spl1 = (0.7071*mid*(slider6)) - (side*0.2); 
spl2=mid*1*(1-slider6); //changed from 1.41 to 1.00 in 2024

spl4 = 0.5071*side; //preupscale

//lopass
slider4<15000?
(
	fd0l = (fk*spl4) - (fa1*fd1l) - (fa0*fd2l);
	spl4 = fd0l + fd1l + fd1l + fd2l;
	fd2l = fd1l;
	fd1l = fd0l;
);

// delay
delaylen>0?
(
	out2 = buf3_org[pos2]  ;
	buf3_org[pos2]     =  spl4;
	spl4=out2;
	pos2=(pos2+1)%(delaylen);
);

// polarity
spl5=spl4*polarity*-1;
	
//depth
do_depth(depth_coeff);

do_width(slider1/100);
