//from https://forum.cockos.com/showthread.php?t=190168#30 by Tale

desc:FFT example
slider1:0.0<-72.0,24.0,0.1>Gain (dB)
slider2:0<-180,180,1>Offset (deg)
slider3:0<0,1,1{25%,50%}>Overlap

@init

size = 4096;
scale = 1/size;

pdc_bot_ch = 0;
pdc_top_ch = 1;
pdc_delay = size - 1;

sixteen_k=16384;

trapezium = sixteen_k*1;
hann		= sixteen_k*2;
in_buf		= sixteen_k*3;
fft_buf		= sixteen_k*4;
out_buf		= sixteen_k*5;


i = 0;
loop(size,
  trapezium[i] = min(2 - abs(4*i / size - 2), 1); // Trapezium window
  hann[i] = 0.5 * (1 - cos(2*$pi * i / size)); // Hann window
  i += 1;
);

@slider

gain = 10^(slider1 / 20);
offset = 2*$pi / 360 * slider2;

window = slider3 < 0.5 ? trapezium : hann;
overlap = floor(size * (window == trapezium ? 0.25 : 0.5));

@sample

// Buffer input
in_buf[overlap + pos] = spl0 * scale;
(pos += 1) >= size - overlap ? (
  pos = 0;

  // Apply window
  i = 0;
  loop(size,
    fft_buf[i*2] = in_buf[i] * window[i];
    fft_buf[i*2 + 1] = 0;
    i += 1;
  );

  // Overlap input
  memcpy(in_buf, in_buf + size - overlap, overlap);

  // FFT
  fft(fft_buf, size);
  fft_permute(fft_buf, size);

  // Adjust gain/phase
  i = 0;
  loop(size / 2 + 1,
    re = fft_buf[i*2];
    im = fft_buf[i*2 + 1];

    mag = sqrt(sqr(re) + sqr(im));
    phase = atan2(im, re);

    mag *= gain;
    phase += offset;

    re = mag * cos(phase);
    im = mag * sin(phase);

    fft_buf[i*2] = re;
    fft_buf[i*2 + 1] = im;

    i > 0 ? (
      fft_buf[(size - i)*2] = re;
      fft_buf[(size - i)*2 + 1] = -im;
    );

    i += 1;
  );

  // Inverse FFT
  fft_ipermute(fft_buf, size);
  ifft(fft_buf, size);

  // Overlap-add output
  memcpy(out_buf, out_buf + size - overlap, overlap);
  memset(out_buf + overlap, 0, size - overlap);
  i = 0;
  loop(size,
    out_buf[i] += fft_buf[i*2];
    i += 1;
  );
);

spl0 = out_buf[pos];